buildscript {
    System.properties['com.android.build.gradle.overrideVersionCheck'] = 'true'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

group = 'com.didichuxing.foundation'
version = '0.0.5'

apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 15
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

android.libraryVariants.all  { variant ->
    def jarTask = project.tasks.create("jar${variant.name.capitalize()}", Jar)
    jarTask.excludes = [
        'android/BuildConfig.class',
        'android/R.class'
    ]
    jarTask.dependsOn variant.javaCompile
    jarTask.from variant.javaCompile.destinationDir
    artifacts.add('archives', jarTask)
}

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                groupId project.group
                artifactId 'android-framework'
                version project.version
                description project.description ?: ''
                packaging 'jar'
            }
            repository(url: MAVEN_RELEASES_REPOSITORY_URL) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }
            snapshotRepository(url: MAVEN_SNAPSHOTS_REPOSITORY_URL) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:22.2.0'
}